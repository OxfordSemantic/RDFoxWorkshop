#Query 12, Bonus
PREFIX : <https://rdfox.com/examples/f1/>

# Can you spot another problem?

# We are returning a lot of drivers who have a high percentage because they only
# competed in very few races.

SELECT ?driverName ?raceCount ?raceWinsFinal ?percentage
WHERE {

# First get the race count for each driver.
    {SELECT ?driver (COUNT(?race) AS ?raceCount)
    WHERE {
    ?result :result_driver ?driver ;
            :result_race ?race .
    }
    GROUP BY ?driver}

# Then get the *win* count for each driver.
OPTIONAL {
    SELECT ?driver (COUNT(?race) AS ?raceWins)
    WHERE {
    ?result :result_driver ?driver ;
            :result_race ?race ;
            :result_positionOrder 1 .
    }
    GROUP BY ?driver
}

FILTER(?raceWins >= 5)

# Use the forename and surname to make the full name
# This is not strictly necessary...
?driver :driver_forename ?forename ;
        :driver_surname ?surname .
BIND(CONCAT(?forename, " ", ?surname) AS ?driverName)
# Using the COALESCE keyword, we make sure that when the number of wins is undefined
# it is 'bound' as 0.
BIND(COALESCE(?raceWins,0) AS ?raceWinsFinal)
# And use the two aggregatee variables to compute a percentage
BIND(?raceWinsFinal/?raceCount AS ?percentage)

}
ORDER BY DESC(?percentage)
