# Query 12, Bonus

# Can you spot another problem?

# We are returning a lot of drivers who have a high percentage because they only
# competed in very few races.

SELECT ?driverName ?raceCount ?raceWinsFinal ?percentage
WHERE {
    {
        SELECT ?driver (COUNT(?race) AS ?raceCount)
        WHERE {
        ?result :result_driver ?driver ;
            :result_race ?race .
        }
        GROUP BY ?driver
    }

    OPTIONAL {
        SELECT ?driver (COUNT(?race) AS ?raceWins)
        WHERE {
            ?result :result_driver ?driver ;
                :result_race ?race ;
                :result_positionOrder 1 .
        }
        GROUP BY ?driver
    }

    FILTER(?raceWins >= 5)

    ?driver :driver_forename ?forename ;
            :driver_surname ?surname .
    BIND(CONCAT(?forename, " ", ?surname) AS ?driverName)

    BIND(COALESCE(?raceWins, 0) AS ?raceWinsFinal)

    BIND(?raceWinsFinal/?raceCount AS ?percentage)
}
ORDER BY DESC(?percentage)
